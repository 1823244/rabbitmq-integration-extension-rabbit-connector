
// Создает и возвращает объект внешней компоненты: AddIn.BITERP.PinkRabbitMQ3
// Выбрасывает исключение
//
// Параметры:
//	нет
//
// Возвращаемое значение:
//	Тип: AddIn.BITERP.PinkRabbitMQ3
//
Функция ПолучитьКлиента() Экспорт  // throws Exception
	
	СистемнаяИнфо = Новый СистемнаяИнформация;
	Если СистемнаяИнфо.ТипПлатформы = ТипПлатформы.Windows_x86 Тогда
		Если НЕ ПодключитьВнешнююКомпоненту("ОбщийМакет.PinkRabbitMQ32", "BITERP", ТипВнешнейКомпоненты.Native) Тогда
			ВызватьИсключение "Ошибка подключения PinkRabbitMQ.dll";
		КонецЕсли;
	ИначеЕсли СистемнаяИнфо.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		Если НЕ ПодключитьВнешнююКомпоненту("ОбщийМакет.PinkRabbitMQ64", "BITERP", ТипВнешнейКомпоненты.Native) Тогда
			ВызватьИсключение "Ошибка подключения PinkRabbitMQ.dll";
		КонецЕсли;
	КонецЕсли;
		
	Клиент  = Новый("AddIn.BITERP.PinkRabbitMQ3");

    Коннект = РегистрыСведений.rabbit_НастройкиПодсистемы.ПолучитьНастройки().АктивныйСерверRabbitMQ;
	
	// В метод Клиент.Connect() нельзя передавать реквизиты справочника!
	// т.е. вот так: Клиент.Connect(Коннект.server,...
	// Будет "Непредвиденная ошибка!"
	// Поэтому сначала помещаем их в переменные:
	server 		= Коннект.server;                                                              
	port 		= Коннект.port;
	username 	= Коннект.username;
	password 	= Коннект.password;
	vhost 		= Коннект.vhost;
	
	Попытка
		Клиент.Connect(server, port, username, password, vhost);
	Исключение
        т = Клиент.GetLastError();
		Клиент = Неопределено;
		ВызватьИсключение т;
	КонецПопытки;
	
		
	Возврат Клиент;
	
КонецФункции



